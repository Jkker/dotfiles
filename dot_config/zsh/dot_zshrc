# Enable Powerlevel10k instant prompt. Should stay close to the top of .zshrc.
[ -f ~/.cache/p10k-instant-prompt-$USERNAME.zsh ] && source ~/.cache/p10k-instant-prompt-$USERNAME.zsh

# 0. znap
[[ -f ~/app/zsh/zsh-snap/znap.zsh ]] || git clone --depth 1 -- \
	https://github.com/marlonrichert/zsh-snap.git ~/app/zsh/zsh-snap
source ~/app/zsh/zsh-snap/znap.zsh

# 1. mise
znap eval mise 'mise activate zsh'

# 2. zsh plugins
znap source marlonrichert/zsh-autocomplete      # fuzzy autocomplete and selection menu.
znap eval zoxide 'zoxide init zsh'              # zoxide for fast directory navigation.
znap source zsh-users/zsh-completions           # additional completions.
znap source ohmyzsh/ohmyzsh lib/git plugins/git # git plugin from ohmyzsh.

ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)    # enable brackets highlighter
znap source zsh-users/zsh-syntax-highlighting # syntax highlighting

ZSH_AUTOSUGGEST_STRATEGY=(history)        # only suggest from history
znap source zsh-users/zsh-autosuggestions # fish-like ghost text suggestions.

# 3. Binary Tools
znap eval carapace 'carapace _carapace'                                    # multi-shell multi-command argument completer
znap eval mise 'mise completions zsh'                                      # polyglot tool version manager completions
znap eval chezmoi 'chezmoi completion zsh'                                 # dotfile manager completions
znap eval uv 'uv generate-shell-completion zsh'                            # fast Python package and project manager completions
znap eval uvx 'uvx --generate-shell-completion zsh'                        # uv tool invoker completions
znap source g-plane/pnpm-shell-completion pnpm-shell-completion.plugin.zsh # pnpm scripts, workspace and options completions
# Ensure pnpm shell completion binary is downloaded
[[ -f ~[g-plane/pnpm-shell-completion]/pnpm-shell-completion ]] || (cd ~[g-plane/pnpm-shell-completion] && ./zplug.zsh)

# 4. History Configuration
HISTFILE="$ZDOTDIR/.zsh_history"
HISTSIZE=100000
SAVEHIST=$HISTSIZE
setopt EXTENDED_HISTORY       # Write the history file in the ':start:elapsed;command' format.
setopt SHARE_HISTORY          # Share history between all sessions.
setopt HIST_EXPIRE_DUPS_FIRST # Expire a duplicate event first when trimming history.
setopt HIST_IGNORE_DUPS       # Do not record an event that was just recorded again.
setopt HIST_IGNORE_ALL_DUPS   # Delete an old recorded event if a new event is a duplicate.
setopt HIST_FIND_NO_DUPS      # Do not display a previously found event.
setopt HIST_IGNORE_SPACE      # Do not record an event starting with a space.
setopt HIST_SAVE_NO_DUPS      # Do not write a duplicate event to the history file.
setopt HIST_VERIFY            # Do not execute immediately upon history expansion.

# 5. Keybindings
# https://github.com/marlonrichert/zsh-edit
WORDCHARS='~*?.' # more precise subword movement in path strings
znap source marlonrichert/zsh-edit

# https://github.com/marlonrichert/zsh-autocomplete#make-tab-and-shifttab-go-to-the-menu
# Make Tab and ShiftTab enter the menu instead of inserting a completion:
bindkey '^I' menu-select
bindkey "$terminfo[kcbt]" menu-select

bindkey -M menuselect '^I' menu-complete
bindkey -M menuselect "$terminfo[kcbt]" reverse-menu-complete

# 6. Aliases & Functions
function copy() {
	if [[ -n "$WSL_DISTRO_NAME" ]]; then
		/mnt/c/Windows/System32/clip.exe
	elif command -v xclip >/dev/null 2>&1; then
		xclip -selection clipboard
	elif command -v pbcopy >/dev/null 2>&1; then
		pbcopy
	else
		echo "No clipboard tool found."
	fi
}

# CLI Tools Aliases
alias fr="fresh"
alias lzd="lazydocker"
alias lzg="lazygit"
alias gy="gemini -y"
alias op="opencode"

# chezmoi & zsh
alias cz="chezmoi"
alias czr="chezmoi re-add"
alias cze="chezmoi edit --apply"
alias czz="chezmoi edit --apply ~/.config/zsh/.zshrc"
alias sz="exec zsh"

alias mx="mise exec --"
alias del='gtrash put'
if command -v eza >/dev/null 2>&1; then
	alias ls='eza --group-directories-first --icons=auto'
	alias tree='ls --tree'
fi

# yazi
function y() {
	local tmp="$(mktemp -t \"yazi-cwd.XXXXXX\")" cwd
	yazi "$@" --cwd-file="$tmp"
	IFS= read -r -d '' cwd <"$tmp"
	[ -n "$cwd" ] && [ "$cwd" != "$PWD" ] && builtin cd -- "$cwd"
	rm -f -- "$tmp"
}

# NPM Aliases
# alias pnpx="pnpm dlx"
alias pp="pnpm"
alias ppab="pnpm approve-builds"
alias ni="pnpm install"
alias nid="pnpm install -D"
function nidt() {
	if [ "$#" -eq 0 ]; then
		echo "Usage: nidt <package-names>"
		return 1
	fi

	local packages=()
	local pkg
	for pkg in "$@"; do
		pkg=${pkg##[[:space:]]#} # trim leading
		pkg=${pkg%%[[:space:]]#} # trim trailing
		[ -n "$pkg" ] && packages+=("@types/${pkg}")
	done

	if [ "${#packages[@]}" -eq 0 ]; then
		echo "Usage: nidt <package-names>"
		return 1
	fi

	local cmd=(pnpm install --save-dev "${packages[@]}")
	echo "Executing: ${cmd[*]}"
	"${cmd[@]}"
}
alias nun="pnpm uninstall"
alias nig="pnpm install -g"
alias nr="pnpm run"
alias nb="nr build"
alias nt="nr test"
alias nl="nr lint"
alias nd="nr dev"
alias ns="nr start"
alias vt="pnpm vitest"
alias gcan="git commit -a --amend --no-edit"
alias vn="pnpx vite-node"
alias npk="rm -rf node_modules package-lock.json yarn.lock pnpm-lock.yaml **/node_modules"

# git Aliases
alias gcl="gh repo clone"
alias gvw="gh repo view --web"
alias gcm="git commit -m"
alias gcmn="git commit --no-verify -m"
alias glg="git log --oneline --graph --decorate --all"
alias gmain="git checkout main"
alias grbmain="git rebase origin/main"
alias gsta="git stash push --include-untracked"

# Python Aliases
alias py="python"

# Editor Setup
if [[ "$TERM_PROGRAM" == "vscode" ]]; then
  export EDITOR="code --wait"
elif command -v fresh >/dev/null 2>&1; then
  export EDITOR="fresh"
else
  export EDITOR="nano"
fi
export VISUAL="$EDITOR"

# Load p10k prompt
znap source romkatv/powerlevel10k powerlevel10k.zsh-theme
[[ ! -f $ZDOTDIR/.p10k.zsh ]] || source $ZDOTDIR/.p10k.zsh
