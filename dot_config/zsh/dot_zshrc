# # Enable Powerlevel10k instant prompt. Should stay close to the top of .zshrc.
() { [[ -r $1 ]] && source $1 } ${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-$USERNAME.zsh

# path setup

export PNPM_HOME="$HOME/.local/share/pnpm"
export PATH=$HOME/.local/bin:$HOME/bin:$PNPM_HOME:$PATH

export ANDROID_HOME="$HOME/android/sdk"
if [[ -d "$ANDROID_HOME" ]]; then
	export PATH="$ANDROID_HOME/emulator:$ANDROID_HOME/tools:$ANDROID_HOME/tools/bin:$ANDROID_HOME/platform-tools:$PATH"
fi

# 0. znap
[[ -f ~/app/zsh/zsh-snap/znap.zsh ]] || git clone --depth 1 -- \
    https://github.com/marlonrichert/zsh-snap.git ~/app/zsh/zsh-snap
source ~/app/zsh/zsh-snap/znap.zsh

# 1. mise
export MISE_GLOBAL_CONFIG_FILE="$HOME/.config/mise/mise.yaml"
export MISE_EXPERIMENTAL=1
znap eval mise 'mise activate zsh'

# 2. zsh plugins
znap eval zoxide 'zoxide init zsh'

znap source marlonrichert/zsh-autocomplete

znap clone zsh-users/zsh-completions ohmyzsh/ohmyzsh
fpath=( ~[zsh-users/zsh-completions]/src $fpath )      # Add completions.

znap source ohmyzsh/ohmyzsh lib/git plugins/git # Load git plugin from ohmyzsh.

ZSH_HIGHLIGHT_HIGHLIGHTERS=( main brackets )
znap source zsh-users/zsh-syntax-highlighting

ZSH_AUTOSUGGEST_STRATEGY=( history )
znap source zsh-users/zsh-autosuggestions

# 3. Binary Tools
# znap eval carapace 'carapace _carapace'
znap eval mise 'mise completions zsh'
znap eval chezmoi 'chezmoi completion zsh'
znap source g-plane/pnpm-shell-completion pnpm-shell-completion.plugin.zsh
if [[ ! -f ~[g-plane/pnpm-shell-completion]/pnpm-shell-completion ]]; then
  (cd ~[g-plane/pnpm-shell-completion] && ./zplug.zsh)
fi

# 4. Settings & Configuration

# History
HISTFILE="$ZDOTDIR/.zsh_history"
HISTSIZE=100000
SAVEHIST=$HISTSIZE
setopt EXTENDED_HISTORY          # Write the history file in the ':start:elapsed;command' format.
setopt SHARE_HISTORY             # Share history between all sessions.
setopt HIST_EXPIRE_DUPS_FIRST    # Expire a duplicate event first when trimming history.
setopt HIST_IGNORE_DUPS          # Do not record an event that was just recorded again.
setopt HIST_IGNORE_ALL_DUPS      # Delete an old recorded event if a new event is a duplicate.
setopt HIST_FIND_NO_DUPS         # Do not display a previously found event.
setopt HIST_IGNORE_SPACE         # Do not record an event starting with a space.
setopt HIST_SAVE_NO_DUPS         # Do not write a duplicate event to the history file.
setopt HIST_VERIFY               # Do not execute immediately upon history expansion.

# 5. Keybindings
# Standard Keybindings (Home/End/Delete) using terminfo for portability
if (( ${+terminfo[kdch1]} )); then bindkey "${terminfo[kdch1]}" delete-char; fi
bindkey "^[[3~" delete-char # Common fallback for Delete
bindkey "^[3;5~" delete-char

if (( ${+terminfo[khome]} )); then bindkey "${terminfo[khome]}" beginning-of-line; fi
bindkey "^[[H" beginning-of-line # Common fallback for Home

if (( ${+terminfo[kend]} )); then bindkey "${terminfo[kend]}" end-of-line; fi
bindkey "^[[F" end-of-line # Common fallback for End

# Ctrl+Left/Right
bindkey "^[[1;5D" backward-word
bindkey "^[[1;5C" forward-word

# Fix CTRL + Backspace
bindkey '^H' backward-kill-word

# 6. Aliases & Functions
# Clipboard
function copy() {
  if [[ -x /mnt/c/Windows/System32/clip.exe ]]; then
    /mnt/c/Windows/System32/clip.exe
  elif command -v wl-copy >/dev/null 2>&1; then
    wl-copy
  elif command -v xclip >/dev/null 2>&1; then
    xclip -selection clipboard
  elif command -v pbcopy >/dev/null 2>&1; then
    pbcopy
  else
    echo "No clipboard tool found."
  fi
}

# CLI Tools Aliases
alias fr="fresh"
alias lzd="lazydocker"
alias lzg="lazygit"
alias cz="chezmoi"
alias mx="mise exec --"
alias del='gtrash put'
if command -v eza >/dev/null 2>&1; then
  alias ls='eza --group-directories-first --icons=auto'
  alias tree='ls --tree'
fi

# yazi
function y() {
	local tmp="$(mktemp -t \"yazi-cwd.XXXXXX\")" cwd
	yazi "$@" --cwd-file="$tmp"
	IFS= read -r -d '' cwd < "$tmp"
	[ -n "$cwd" ] && [ "$cwd" != "$PWD" ] && builtin cd -- "$cwd"
	rm -f -- "$tmp"
}

# NPM Aliases (with ni)
alias pnpx="pnpm dlx"
alias pp="pnpm"
alias ppab="pnpm approve-builds"
alias ni="pnpm install"
alias nid="pnpm install -D"
function nidt() {
	if [ "$#" -eq 0 ]; then
		echo "Usage: nidt <package-names>"
		return 1
	fi

	local packages=()
	local pkg
	for pkg in "$@"; do
		pkg=${pkg##[[:space:]]#} # trim leading
		pkg=${pkg%%[[:space:]]#} # trim trailing
		[ -n "$pkg" ] && packages+=("@types/${pkg}")
	done

	if [ "${#packages[@]}" -eq 0 ]; then
		echo "Usage: nidt <package-names>"
		return 1
	fi

	local cmd=(pnpm install --save-dev "${packages[@]}")
	echo "Executing: ${cmd[*]}"
	"${cmd[@]}"
}
alias nun="pnpm uninstall"
alias nig="pnpm install -g"
alias nr="pnpm run"
alias nb="nr build"
alias nt="nr test"
alias nrl="nr lint"
alias nrt="nr typecheck"
alias nd="nr dev"
alias ns="nr start"
alias vt="pnpm vitest"
alias gcan="git commit -a --amend --no-edit"
alias vn="pnpx vite-node"
alias npk="rm -rf node_modules package-lock.json yarn.lock pnpm-lock.yaml **/node_modules"

# git Aliases
alias gcl="gh repo clone"
alias gvw="gh repo view --web"
alias gcm="git commit -m"
alias gcmn="git commit --no-verify -m"
alias glg="git log --oneline --graph --decorate --all"
alias gmain="git checkout main"
alias grbmain="git rebase origin/main"
alias gsta="git stash push --include-untracked"

# Python Aliases
alias py="python"

# ZSH Aliases
alias sz="exec zsh"
alias gy="gemini -y"

# Editor Configuration
if [[ "$TERM_PROGRAM" == "vscode" ]]; then
  export EDITOR="code --wait"
elif command -v fresh >/dev/null 2>&1; then
  export EDITOR="fresh"
else
  export EDITOR="nano"
fi
export VISUAL="$EDITOR"

# WSL Specific Aliases & Configuration
if command -v wslinfo >/dev/null 2>&1 || [[ -n "$WSL_DISTRO_NAME" ]]; then
	export BROWSER=wslview

	# Add Windows tools to PATH
	if command -v wslvar >/dev/null 2>&1 && command -v wslpath >/dev/null 2>&1; then
		local local_appdata=$(wslpath "$(wslvar LOCALAPPDATA)")
		if [[ -n "$local_appdata" ]]; then
			# ADB Platform Tools
			local adb_path="$local_appdata/Microsoft/WinGet/Packages/Google.PlatformTools_Microsoft.Winget.Source_8wekyb3d8bbwe/platform-tools"
			if [[ -d "$adb_path" ]]; then
				export PATH="$PATH:$adb_path"
			fi
			# Antigravity
			local antigravity_path="$local_appdata/Programs/Antigravity"
			if [[ -d "$antigravity_path" ]]; then
				alias antigravity="$antigravity_path/bin/antigravity"
			fi
			# VSCode
			local vscode_path="/mnt/c/Program Files/Microsoft VS Code/bin"
			if [[ -d "$vscode_path" ]]; then
				export PATH="$PATH:$vscode_path"
			fi

		fi
	fi

	# Aliases
	alias xdg-open='wslview'
	alias explorer='/mnt/c/WINDOWS/explorer.exe'
	alias open="explorer"

	alias pwsh="'/mnt/c/Program Files/PowerShell/7/pwsh.exe'"
	alias adb="adb.exe -d"
	alias fastboot="fastboot.exe"
fi

# Load p10k prompt
znap source romkatv/powerlevel10k powerlevel10k.zsh-theme
[[ ! -f $ZDOTDIR/.p10k.zsh ]] || source $ZDOTDIR/.p10k.zsh

